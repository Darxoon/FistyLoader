BITS 64

section .fisty vstart=0x1ab7000
; org 0x1ab7000

; constants
NULL equ 0
STD_OUTPUT_HANDLE equ -11

msg db "Hello World", 0Dh, 0Ah
msgLen equ $-msg

; runtime data
alignb 8
written dq 0

; code
env_init_hook:
    push rcx
    push rbp
    push rdx
    push r8
    push r9
    push r10
    push r11
    
    ; SDL_ShowSimpleMessageBox
    mov ecx, 0x40
    lea rdx, [rel msgTitle]
    lea r8, [rel msgContent]
    xor r9, r9
    call env_init_hook-0x1435198
    
    pop r11
    pop r10
    pop r9
    pop r8
    pop rdx
    pop rbp
    pop rcx
    
    ; softbranch
    push rbp
    push rbx
    push rsi
    push rdi
    
    ; lea rax, qword [rel env_init_hook-0x182F6E3]
    jmp env_init_hook-0x182F6E3

load_config_hook:
    push rbx
    push rcx
    push rdx
    push rbp
    push r8
    push r9
    push r10
    push r11
    push r12
    
    mov rbp, rsp
    sub rsp, 32 + 128
    
    ; FileSystemUtils::CreateDir
    lea rcx, [rel fistyPath]
    call load_config_hook-0x1858162
    
    ; Environment::instance
    call load_config_hook-0x1890FC2
    
    ; Environment::getStorage (vtable[0x28])
    mov rdx, qword [rax] ; rdx = env->vtable
    mov rcx, rax
    call qword [rdx + 0x140]
    mov rbx, rax ; rbx = SDL2Storage* storage
    
    ; SDL2Storage::FileExists (vtable[1])
    ; I'm just going to ignore the TOCTOU "vulnerability" here
    mov r12, qword [rbx] ; r12 = storage->vtable
    mov rcx, rbx ; this
    lea rdx, [rel ballTablePath] ; filePath
    call qword [r12+0x8]
    test al, al ; for some reason FileExists outputs in al only
    jne load_config_hook_merge ; skip the following code if ballTable.ini exists already
    
    ; SDL2Storage::FileOpen (vtable[2])
    mov rcx, rbx ; this
    lea rdx, [rel ballTablePath] ; filePath
    mov r8, 0x22 ; flags (0x22 : "w+b")
    lea r9, [rbp-0x10] ; out_fileHandle
    call qword [r12+0x10]
    
    ; if (result == 0) continue else load_config_hook_failure
    test rax, rax
    jne load_config_hook_failure
    ; FileOpen succeded
    ; SDL2Storage::FileWrite (vtable[4])
    mov rcx, rbx ; this
    mov rdx, [rbp-0x10] ; fileHandle
    lea r8, [rel ballTableHeader] ; content
    mov r9, ballTableHeaderLen ; size
    call qword [r12+0x20]
    
    ; for (int i = 0; i < GooBallType::COUNT; i++)
    mov r12, 0 ; r12 = no longer vtable, now int i
load_config_hook_loop_start:
    lea rcx, [rel load_config_hook-0xF94B62] ; gooballIds
    mov rcx, [rcx + r12 * 8]
    mov [rsp+0x20], rcx
    
    ; snprintf
    lea rcx, [rbp-0x90] ; dst
    mov rdx, 0x80 ; = 128, n
    lea r8, [rel ballTableLineFormat] ; format
    mov r9, r12 ; var arg 0
    call load_config_hook-0x1A62732
    
    ; SDL2Storage::FileWrite (vtable[4])
    mov rcx, rbx ; this
    mov rdx, [rbp-0x10] ; fileHandle
    lea r8, [rbp-0x90] ; content
    mov r9, rax ; size
    mov rax, qword [rbx]
    call qword [rax+0x20]
    
    add r12, 1
    cmp r12, baseGooballCount
    jl load_config_hook_loop_start
    
    ; end of loop
    ; SDL2Storage::FileClose (vtable[5])
    mov rcx, rbx ; this
    mov rdx, [rbp-0x10] ; fileHandle
    mov rax, qword [rbx]
    call qword [rax+0x28]
    
    ; SDL_ShowSimpleMessageBox
    mov ecx, 0x40
    lea rdx, [rel msgTitle]
    lea r8, [rel msgBallTableSuccess]
    xor r9, r9
    call env_init_hook-0x1435198
    
    jmp load_config_hook_merge

load_config_hook_failure:
    ; Show error message that ballTable.ini could not be created
    ; SDL_ShowSimpleMessageBox
    mov ecx, 0x10
    lea rdx, [rel msgTitle]
    lea r8, [rel msgBallTableErr]
    xor r9, r9
    call env_init_hook-0x1435198
    
load_config_hook_merge:
    add rsp, 32 + 128
    
    pop r12
    pop r11
    pop r10
    pop r9
    pop r8
    pop rbp
    pop rdx
    pop rcx
    pop rbx
    
    ; softbranch
    mov qword [rsp+0x10], rbx
    jmp load_config_hook-0x182E5BD

; constants 2 (so I don't shift the asm around)
msgTitle db "Fisty Loader", 00h
msgContent db "This is a test", 00h
msgBallTableErr db \
    "Failed to create ballTable.ini file. Make sure the game directory is not ",\
    "inside C:\Program Files or any other place that requires administrator permissions.", 00h
msgBallTableSuccess db \
    "Successfully extracted assets from exe file into 'World of Goo 2 (current installation's game directory)/game/fisty'", 00h

fistyPath db "fisty", 00h
ballTablePath db "fisty/ballTable.ini", 00h
ballTableHeader db \
    "; This table defines all Gooball typeEnums", 0Dh, 0Ah, \
    "; Extend this list to add your own gooballs.", 0Dh, 0Ah, \
    "; ", 0Dh, 0Ah, \
    "; Generated by FistyLoader 0.1", 0Dh, 0Ah, 0Dh, 0Ah
ballTableHeaderLen equ $-ballTableHeader
db 0

ballTableLineFormat db "%d=%s", 0Dh, 0Ah, 00h

baseGooballCount equ 39

; in order to pad the file to the correct size (0x3000)
section .ignoreme start=0x1ab9ffc
dd 0
